import {unitt}!

import.lean {src/html.art}!

suite "<base/>" [

    test "<base/> must have 'href, 'target or both" [
        base: $[x][fragment [base x]]

        withHref: #[href: "https://example.com"]
        withTarget: #[target: "_blank"]
        withBoth: extend withHref withTarget

        assert -> throws? [_: base #[]]

        loop @[withHref withTarget withBoth] 'dict [
            assert -> not? throws? [_: fragment -> base] ++ dict
        ]

    ]

    test "<base/> must be void and follow the standard" [
        expect: {!html
            <base href="https://example.com" target="_blank"/>
        }

        result: fragment [base #[href: "https://example.com" target: "_blank"]]
        assert -> expect = result
    ]
]


suite "<link/>" [

    test "<link/> takes :string-like arguments" [
        link: $[x y][fragment [metaLink x y]]

        assert -> not? throws? [_: link "stylesheet" "style.css"]
        assert -> not? throws? [_: link :stylesheet "style.css"]
        assert -> not? throws? [_: link 'stylesheet "style.css"]

        assert -> throws? [_: link ["stylesheet"] "style.css"]
        assert -> throws? [_: link :stylesheet ["style.css"]]
        assert -> throws? [_: link ['stylesheet] ["style.css"]]
    ]

    test "<link/> .size gets an :integer and converts to standard" [
        expect: {!html
            <link rel="apple-touch-icon" href="icon.png" size="70x70"/>
        }

        result: fragment [
            metaLink.size: 70 "apple-touch-icon" "icon.png"
        ]

        assert -> expect = result
    ]

    test "<link/>'s .size does not influences other attributes" [
        expect: {!html
            <link rel="apple-touch-icon" href="icon.png" size="70x70" type="image/png"/>
        }

        result: fragment [
            metaLink.size: 70 .type: "image/png" "apple-touch-icon" "icon.png"
        ]

        assert -> expect = result
    ]

    test "<link/> is a void element and follows the standard" [
        expect: {!html
            <head>
              <title></title>
              <link rel="icon" href="icon.ico"/>
              <link rel="stylesheet" href="style.css"/>
              <link rel="apple-touch-icon" href="icon.png" size="70x70"/>
            </head>
        }

        result: fragment [
            head [
                title ""
                link :icon "icon.ico"
                link :stylesheet "style.css"
                link.size: 70 "apple-touch-icon" "icon.png"
            ]
        ]

        assert -> expect = result
    ]

]

suite "<meta/>" [

    test "<meta/> must have attributes" [
        assert -> not? throws? [_: fragment [meta #[charset: "utf-8"]]]
        assert -> throws? [_: fragment [meta #[]]]
    ]

    test "<meta/> is a void element and gets a :dictionary" [
        expect: {!html
            <head>
              <title></title>
              <meta charset="utf-8"/>
              <meta http-equiv="content-security-policy" content="..."/>
              <meta name="twitter:card" content="summary"/>
            </head>
        }

        result: fragment [
            head [
                title ""
                meta #[charset: "utf-8"]
                meta #["http-equiv": "content-security-policy" content: "..."]
                meta #[name: "twitter:card" content: "summary"]
            ]
        ]

        assert -> expect = result
    ]
]

suite "<style/>" [

    test.skip "<style/> only gets a :string" [
        assert -> not? throws? [artml\style ""]
        assert -> throws? [artml\style [""]]
    ]

    test.skip "<style/> is a normal element and follows the standard" [
        expect: {!html
            <style media="max-width: 720px">
              p {
                  color: black;
              }
            </style>
        }

        result: artml\style {!css
            p {
                color: black;
            }
        }

        assert -> expect = result

    ]

]

suite "<title/>" [

    test.skip "<title/> is a normal element and follows the standard" [
        expect: {!html
            <title>Arturo Language!</title>
        }

        result: artml\title "Arturo Language!"
        assert -> expect = result

    ]

]
